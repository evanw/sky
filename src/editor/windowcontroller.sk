namespace Editor {
  interface WindowObserver {
    def handleAction(action Action)
    def handleFrame
  }

  class WindowController :: UI.WindowDelegate, DocumentStoreObserver {
    const _window UI.Window
    const _observers List<WindowObserver> = []
    const _documentStore = DocumentStore.new
    const _views List<View> = []
    var _currentView View = null
    var _nextUntitledID = 1
    var _viewY = 0.0

    def new(platform UI.Platform) {
      var window = platform.createWindow
      window.setFont(.CODE_FONT, MONOSPACE_FONTS, 12, 16, 0)
      window.setFont(.MARGIN_FONT, MONOSPACE_FONTS, 10, 14, 0)
      window.setFont(.UI_FONT, SANS_SERIF_FONTS, 12, 16, 0)
      window.setDelegate(self)
      _window = window

      var themes List<UI.Theme> = [
        .ATOM,
        # .BRACKETS_DARK,
        # .BRACKETS_LIGHT,
        # .EARTHSONG,
        # .IDLE,
        # .MONOKAI,
        # .TWILIGHT,
        # .VISUAL_STUDIO_DARK,
        # .VISUAL_STUDIO_LIGHT,
        # .XCODE,
      ]
      for theme in themes {
        var tabs = TabsView.new(self)
        tabs.setTheme(theme)
        tabs.setLayout(.STRETCH, 0, 0, .MIN, _viewY, _viewY + theme.tabAreaHeight)
        _viewY += theme.tabAreaHeight
        tabs.appendTo(window.root)
      }

      _documentStore.addObserver(self)
      _documentStore.appendDocument(createNewDocument("Untitled"))
    }

    def window UI.Window {
      return _window
    }

    def documentStore DocumentStore {
      return _documentStore
    }

    def createNewDocument(name string) Model {
      var document = Model.new(window.platform, window.renderer.fontInstance(.CODE_FONT), 2)
      document.setName(name)
      document.setLexer(.SKEW)
      return document
    }

    def addObserver(observer WindowObserver) {
      _observers.appendOne(observer)
    }

    def removeObserver(observer WindowObserver) {
      _observers.removeOne(observer)
    }

    def triggerFrame {
      for observer in _observers {
        observer.handleFrame
      }
    }

    def triggerAction(action Action) {
      _handleAction(action)

      # Avoid issues with mutation during iteration if the
      # list of observers changes while handling the action
      for observer in _observers.clone {
        observer.handleAction(action)
      }
    }

    def _changeActiveIndex(index int) {
      if index >= 0 && index < _documentStore.documents.count {
        _documentStore.setActiveIndex(index)
      }
    }

    def _handleAction(action Action) {
      var count = _documentStore.documents.count
      var activeIndex = _documentStore.activeIndex
      var filePicker = window.filePicker

      switch action {
        case .NEW_DOCUMENT {
          _documentStore.appendDocument(createNewDocument("Untitled \(++_nextUntitledID)"))
        }

        case .CLOSE_DOCUMENT {
          if activeIndex != -1 {
            _documentStore.removeDocument(activeIndex)
          }
        }

        case .OPEN {
          if filePicker != null {
            filePicker.pickOpenPath(null, files => {
              for file in files {
                Log.info("opening file: \(file.path)")
                var text = file.readText
                if text != null {
                  var model = createNewDocument(null)
                  model.setPath(file.path)
                  model.setContents(file.readText)
                  _documentStore.appendDocument(model)
                }
              }
            })
          }
        }

        case .SAVE, .SAVE_AS {
          if filePicker != null && activeIndex != -1 {
            var model = _documentStore.documents[activeIndex]
            var contents = model.toString

            # Try saving without a dialog first
            if action == .SAVE && model.path != null {
              var file = filePicker.fileFromPath(model.path)
              if file != null {
                Log.info("saving file: \(file.path)")
                file.writeText(contents)
                return
              }
            }

            var directory = model.path != null ? IO.Path.split(model.path).directory : null
            filePicker.pickSavePath(directory, file => {
              Log.info("saving file: \(file.path)")
              file.writeText(contents)
              model.setPath(file.path)
            })
          }
        }

        # Tab navigation actions
        case .TABS_PREVIOUS { _changeActiveIndex((activeIndex - 1) %% count) }
        case .TABS_NEXT { _changeActiveIndex((activeIndex + 1) % count) }
        case .TABS_1 { _changeActiveIndex(0) }
        case .TABS_2 { _changeActiveIndex(1) }
        case .TABS_3 { _changeActiveIndex(2) }
        case .TABS_4 { _changeActiveIndex(3) }
        case .TABS_5 { _changeActiveIndex(4) }
        case .TABS_6 { _changeActiveIndex(5) }
        case .TABS_7 { _changeActiveIndex(6) }
        case .TABS_8 { _changeActiveIndex(7) }
        case .TABS_LAST { _changeActiveIndex(count - 1) }
      }
    }

    def _createViewForDocument(document Model) View {
      var view = View.new(self)

      if BUILD != .TERMINAL {
        view.changePadding(5, 5, 5, 5)
        view.changeMarginPadding(15, 5)
        view.setLayout(.STRETCH, 0, 0, .STRETCH, _viewY, 0)
      }

      else {
        view.setScrollbarThickness(1)
        view.changePadding(0, 0, 1, 0) # Leave one character of padding on the right so the caret can be seen at the end of the line
        view.changeMarginPadding(1, 1)
      }

      view.setModel(document)
      view.setTheme(.ATOM)
      return view
    }

    def handleDocumentStoreChange(moved Model, from int, to int) {
      # Keep the list of views in sync with the list of documents
      if moved != null {
        if from == -1 { _views.insert(to, _createViewForDocument(moved)) }
        else if to == -1 { _views.takeAt(from).destroy }
        else { _views.insert(to, _views.takeAt(from)) }
      }

      # Make sure the active view reflects the active document
      var view = _documentStore.activeIndex != -1 ? _views[_documentStore.activeIndex] : null
      if view != _currentView {
        if _currentView != null {
          _currentView.removeFromParent
        }
        _currentView = view
        if view != null {
          view.appendTo(window.root)
          window.focusView(view)
          window.setTitle("\(view.model.name) - \(TITLE)")
        } else {
          window.setTitle(TITLE)
        }
      }

      _window.invalidate
    }
  }

  namespace WindowController {
    const TITLE = "Sky Text Editor"

    const MONOSPACE_FONTS = [
      "Monaco",
      "Menlo",
      "Consolas",
      "Courier New",
      "monospace",
    ]

    const SANS_SERIF_FONTS = [
      "San Francisco",
      "Lucida Grande",
      "Segoe UI",
      "Arial",
      "sans-serif",
    ]
  }
}
