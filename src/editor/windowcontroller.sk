namespace Editor {
  interface WindowObserver {
    def handleAction(action Action)
    def handleFrame
  }

  class WindowController :: UI.WindowDelegate {
    const _window UI.Window
    const _observers List<WindowObserver> = []

    def new(platform UI.Platform) {
      var window = platform.createWindow
      window.setTitle("Untitled - Sky")
      window.setTheme(.XCODE)
      # window.setFont(.CODE_FONT, MONOSPACE_FONTS, 12, 16)
      window.setFont(.CODE_FONT, SANS_SERIF_FONTS, 12, 16)
      window.setFont(.MARGIN_FONT, MONOSPACE_FONTS, 10, 14)
      window.setFont(.UI_FONT, SANS_SERIF_FONTS, 12, 16)
      _window = window

      var model = Model.new("Untitled", platform, window.renderer.fontInstance(.CODE_FONT), 8)
      model.setLexer(.SKEW)

      var view = View.new(self, model)
      if BUILD != .TERMINAL {
        view.changePadding(5, 5, 5, 5)
        view.changeMarginPadding(15, 5)
      } else {
        view.setScrollbarThickness(1)
        view.changePadding(0, 0, 1, 0) # Leave one character of padding on the right so the caret can be seen at the end of the line
        view.changeMarginPadding(1, 1)
      }
      view.appendTo(window.root)

      # Include some unicode text for testing in debug mode
      if !RELEASE {
        view.insertText("{\n" +
          "\t\"strings\": [\n" +
          "\t\t\"Ğ¦Ğ»Ñ—Ï‚ÏƒÎ´Îµ\",\n" +
          "\t\t\"ï¾Œï¾‘ï½±ï¾‘ï½¶ï¾“ï½¹ï¾“\",\n" +
          "\t\t\"ãƒ•ãƒ ãƒ¤ãƒ ã‚«ãƒ¢ã‚±ãƒ¢\",\n" +
          "\t\t\"ãŠ€ãŠãŠ‚ãŠƒãŠ„\",\n" +
          "\t\t\"â˜³â˜¶â˜²\",\n" +
          "\t\t\"ğ¡‡™ğŒ†\",\n" +
          "\t\t\"ğŸ™ˆğŸ™‰ğŸ™Š\",\n" +
          "\t\t\"spÉ¹ÉÊÊÉ”Éq puÉ uÊop Çpá´‰sdn sá´‰ Ê‡xÇÊ‡ sá´‰É¥Ê‡\",\n" +
          "\t\t\"Â¯\\_(ãƒ„)_/Â¯\",\n" +
          "\t\t\"(â•¯Â°â–¡Â°ï¼‰â•¯ï¸µ â”»â”â”»\",\n" +
          "\t\t\"â”¬â”€â”€â”¬ ãƒ(ã‚œ-ã‚œãƒ)\",\n" +
          "\t\t\"à² _à² \"\n" +
          "\t]\n" +
          "}\n"
        )
      }

      window.setDelegate(self)
      window.focusView(view)
    }

    def window UI.Window {
      return _window
    }

    def addObserver(observer WindowObserver) {
      _observers.appendOne(observer)
    }

    def removeObserver(observer WindowObserver) {
      _observers.removeOne(observer)
    }

    def triggerFrame {
      for observer in _observers {
        observer.handleFrame
      }
    }

    def triggerAction(action Action) {
      for observer in _observers {
        observer.handleAction(action)
      }
    }
  }

  namespace WindowController {
    const MONOSPACE_FONTS = [
      "Menlo-Regular",
      "Monaco",
      "Consolas",
      "Courier New",
      "monospace",
    ]

    const SANS_SERIF_FONTS = [
      "San Francisco",
      "Lucida Grande",
      "Segoe UI",
      "Arial",
      "sans-serif",
    ]
  }
}
