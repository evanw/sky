namespace Editor {
  class Span {
    const start int
    const end int
    const offsetX double
    const advanceWidth double
    const color UI.Color
  }

  namespace Span {
    const ORDER_BY_START = (a Span, b Span) => a.start <=> b.start
  }

  class LexerState {
    def equals(other LexerState) bool {
      return other == self
    }
  }

  # A resumable lexer that styles lines of text. Custom lexers can easily be
  # created by creating a new Lexer. There is an optional state system if
  # you need to track information across lines. For example, a multi-line
  # comment would use a different lexer state for the next line than a single-
  # line comment. For simple enum-style state, just creating a fixed number of
  # LexerState objects and check for equality. More advanced usage can be done
  # by subclassing LexerState, adding extra fields, and overriding equals().
  class Lexer {
    const _tokenizeLine fn(Lexer)
    var _spans List<Span> = null
    var _state LexerState = null
    var _current Line = null
    var _currentIndex = 0
    var _nextCodePoint = 0
    var _limit = 0
    var _startOfState = 0
    var _iterator = UI.AdvanceWidthIterator.new

    def styleLine(line Line, previousState LexerState) {
      _state = previousState
      _iterator.reset(line.text)
      _current = line
      _currentIndex = 0
      _nextCodePoint = -1
      _startOfState = 0
      _limit = line.text.count
      _spans = []
      _tokenizeLine(self)

      # Make sure all tabs have spans since they are variable width
      var previousIndex = 0
      var count = _spans.count
      for i in 0..count {
        var span = _spans[i]
        assert(previousIndex <= span.start) # Tokenized spans must be sorted
        _addSpansForTabs(previousIndex, span.start)
        previousIndex = span.end
      }
      _addSpansForTabs(previousIndex, _limit)
      if _spans.count != count {
        _spans.sort(Span.ORDER_BY_START)
      }

      line.previousState = previousState
      line.nextState = _state
      line.spans = _spans
      assert(_state != null)
    }

    def setFont(font UI.FontInstance, indent int) {
      _iterator.setFont(font, indent)
    }

    def currentText string {
      return _current.text
    }

    def currentIndex int {
      return _currentIndex
    }

    def currentState LexerState {
      return _state
    }

    def endOfLine int {
      return _limit
    }

    def hasNext bool {
      return _currentIndex < _limit
    }

    def next {
      assert(_iterator.currentIndex >= _currentIndex)
      if _currentIndex == _iterator.currentIndex {
        _iterator.nextCodePoint
      }
      _currentIndex = _iterator.currentIndex
    }

    def startOfState int {
      return _startOfState
    }

    def peekNext int {
      _loadNextIfNeeded
      return _nextCodePoint
    }

    def takeNext int {
      var c = peekNext
      next
      return c
    }

    def matchNext(c int) bool {
      if peekNext == c {
        next
        return true
      }
      return false
    }

    def scanAlphaNumericString string {
      var text = ""
      while hasNext {
        var c = peekNext
        if !Lexer.isAlphaOrDigit(c) {
          break
        }
        next
        text += string.fromCodeUnit(c)
      }
      return text
    }

    def transitionToState(state LexerState, startingIndex int) {
      _state = state
      _startOfState = startingIndex
    }

    def addSpan(start int, end int, color UI.Color) {
      assert(0 <= start && start < end && end <= _limit)

      # Get the start position
      _iterator.seekTo(start)
      var previousIndex = start
      var previousX = _iterator.advanceWidthFromLeft

      # Split the span at each tab character
      while _iterator.currentIndex < end {
        var nextIndex = _iterator.currentIndex
        var nextX = _iterator.advanceWidthFromLeft
        var codePoint = _iterator.nextCodePoint

        # Tab width depends on context so separate it out into its own span
        if codePoint == '\t' {
          _spans.append(Span.new(previousIndex, nextIndex, previousX, nextX - previousX, color))
          previousIndex = _iterator.currentIndex
          previousX = _iterator.advanceWidthFromLeft
          _spans.append(Span.new(nextIndex, previousIndex, nextX, previousX - nextX, color))
        }
      }

      # Add a final span at the end
      if previousIndex < end {
        _spans.append(Span.new(previousIndex, _iterator.currentIndex, previousX, _iterator.advanceWidthFromLeft - previousX, color))
      }

      # Don't disrupt lexing
      _iterator.seekTo(_currentIndex)
    }

    def changePreviousSpanColor(color UI.Color) {
      var previous = _spans.last
      _spans.last = Span.new(previous.start, previous.end, previous.offsetX, previous.advanceWidth, color)
    }

    def _loadNextIfNeeded {
      assert(_iterator.currentIndex >= _currentIndex)
      if _iterator.currentIndex == _currentIndex {
        _nextCodePoint = _iterator.nextCodePoint
      }
    }

    def _addSpansForTabs(start int, end int) {
      var line = _current
      for i in start..end {
        if line.text[i] == '\t' {
          addSpan(i, i + 1, .FOREGROUND_DEFAULT)
        }
      }
    }
  }

  namespace Lexer {
    def isSpace(c int) bool {
      return c == ' ' || c == '\t'
    }

    def isDigit(c int) bool {
      return c >= '0' && c <= '9'
    }

    def isUpperCase(c int) bool {
      return c >= 'A' && c <= 'Z'
    }

    def isLowerCase(c int) bool {
      return c >= 'a' && c <= 'z'
    }

    def isAlpha(c int) bool {
      return isUpperCase(c) || isLowerCase(c) || c == '_'
    }

    def isAlphaOrDigit(c int) bool {
      return isAlpha(c) || isDigit(c)
    }

    def hasLowerCase(text string) bool {
      for i in 0..text.count {
        if isLowerCase(text[i]) {
          return true
        }
      }
      return false
    }
  }
}
