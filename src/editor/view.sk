namespace Editor {
  enum Axis {
    X
    Y
  }

  enum MotionAction {
    CHANGE_SELECTION
    EXTEND_SELECTION
    DELETE_SELECTION
  }

  enum Direction {
    PREVIOUS
    NEXT

    def delta int {
      return self == PREVIOUS ? -1 : 1
    }
  }

  enum StepX {
    CHARACTER
    WORD
    LINE
  }

  enum StepY {
    LINE
    PAGE
    DOCUMENT
  }

  enum ViewSetting {
    SCROLL_PAST_END
    SHOW_MARGIN
  }

  enum MouseMode {
    NONE
    SELECTING_CHARACTERS
    SELECTING_LINES
    SELECTING_BOX
  }

  enum SelectionMerge {
    NONE
    UNION
    EXTEND
  }

  # Each view is backed by a model and manages keyboard input, mouse input,
  # scrolling, and rendering. All pixel values are floating-point to provide
  # a completely scalable UI. This shouldn't prevent use as a terminal-based
  # editor, however, since fractional values should only occur when the view
  # is given fractional inputs. Terminal-based editors can just use integers
  # and everything will still work.
  class View : UI.View :: ModelObserver, WindowObserver {
    const _controller WindowController
    const _iterator = UI.AdvanceWidthIterator.new
    var _model Model
    var _selection = Selection.new([.EMPTY])
    var _areCaretsVisible = false
    var _caretBlinkStartTime = 0.0
    var _isSelectingInTerminal = false
    var _composingText string = null
    var _idleCount = 0

    # Mouse handling
    var _previousSelection Selection = null
    var _previousViewLocation Vector = null
    var _mergeBehavior SelectionMerge = .NONE
    var _draggingScrollbar Scrollbar = null
    var _mouseMode MouseMode = .NONE
    var _mouseStep StepX = .CHARACTER
    var _draggingStart Vector = null
    var _draggingMarker Marker = null
    var _draggingRegion Region = null
    var _scrollbarX = Scrollbar.new(.X)
    var _scrollbarY = Scrollbar.new(.Y)

    # Measurements
    var _viewWidth = 0.0
    var _viewHeight = 0.0
    var _paddingLeft = 0.0
    var _paddingTop = 0.0
    var _paddingRight = 0.0
    var _paddingBottom = 0.0
    var _lineHeight = 1.0
    var _defaultSpaceAdvanceWidth = 1.0
    var _marginSpaceAdvanceWidth = 1.0
    var _marginWidth = 0.0
    var _marginPaddingLeft = 0.0
    var _marginPaddingRight = 0.0
    var _scrollbarThickness = 11.0

    var _settings IntMap<bool> = {
      ViewSetting.SCROLL_PAST_END: true,
      ViewSetting.SHOW_MARGIN: true,
    }

    def new(controller WindowController, model Model) {
      super(controller.window)
      _controller = controller
      controller.addObserver(self)
      setModel(model)

      var renderer = window.renderer
      var defaultFont = renderer.fontInstance(.CODE_FONT)
      _defaultSpaceAdvanceWidth = defaultFont.advanceWidth(SPACE_CHARACTER)
      _marginSpaceAdvanceWidth = renderer.fontInstance(.MARGIN_FONT).advanceWidth(SPACE_CHARACTER)
      _lineHeight = defaultFont.lineHeight
      _iterator.setFont(defaultFont, model.currentIndent)

      _scrollbarX.setMinimumSize(_lineHeight * 3)
      _scrollbarY.setMinimumSize(_lineHeight * 3)
      _updateScrollSizeAndInvalidate(.NORMAL)
      _invalidate(.RESET_CARET_BLINK_TIME)
    }

    def model Model {
      return _model
    }

    def marginWidth double {
      return _marginWidth
    }

    def setting(setting ViewSetting) bool {
      assert(setting in _settings)
      return _settings[setting]
    }

    def changeSetting(setting ViewSetting, value bool) {
      _settings[setting] = value
      _updateScrollSizeAndInvalidate(.NORMAL)
    }

    def changePadding(left double, top double, right double, bottom double) {
      _paddingLeft = left
      _paddingTop = top
      _paddingRight = right
      _paddingBottom = bottom
      _updateScrollSizeAndInvalidate(.NORMAL)
    }

    def changeMarginPadding(left double, right double) {
      _marginPaddingLeft = left
      _marginPaddingRight = right
      _updateScrollSizeAndInvalidate(.NORMAL)
    }

    def setScrollbarThickness(thickness double) {
      _scrollbarThickness = thickness
      _updateScrollSizeAndInvalidate(.NORMAL)
    }

    over updateBounds {
      super
      _viewWidth = bounds.width
      _viewHeight = bounds.height
      _updateScrollSizeAndInvalidate(.NORMAL)
    }

    def _handleScroll(deltaX double, deltaY double) {
      var updatedX = _scrollbarX.setScrollOffset(_scrollbarX.scrollOffset + deltaX)
      var updatedY = _scrollbarY.setScrollOffset(_scrollbarY.scrollOffset + deltaY)

      if updatedX || updatedY {
        _invalidate(.NORMAL)

        if _previousViewLocation != null {
          handleMouseMove(_previousViewLocation)
        }
      }
    }

    def _modelLocation(viewLocation Vector) Vector {
      return Vector.new(
        viewLocation.x + _scrollbarX.scrollOffset - _paddingLeft - _marginWidth,
        Math.clamp(Math.floor((viewLocation.y + _scrollbarY.scrollOffset - _paddingTop) / _lineHeight) as int, 0, _model.count - 1))
    }

    over handleEvent(event UI.Event) {
      switch event.type {
        case .MOUSE_DOWN {
          var viewLocation = event.mouseEvent.locationInView(self)
          handleMouseMove(viewLocation)
          window.focusView(self)

          if _scrollbarX.startDragging(viewLocation, event.mouseEvent.modifiers) {
            _draggingScrollbar = _scrollbarX
          }

          else if _scrollbarY.startDragging(viewLocation, event.mouseEvent.modifiers) {
            _draggingScrollbar = _scrollbarY
          }

          else {
            _draggingScrollbar = null
            _mergeBehavior =
              window.platform.baseModifier in event.mouseEvent.modifiers ? .UNION :
              .SHIFT in event.mouseEvent.modifiers ? .EXTEND :
              .NONE
            _previousSelection = _selection
            _mouseMode =
              event.mouseEvent.locationInWindow.x < _marginWidth ? .SELECTING_LINES :
              .ALT in event.mouseEvent.modifiers ? .SELECTING_BOX :
              .SELECTING_CHARACTERS
            _mouseStep = ((event.mouseEvent.clickCount + 2) % 3) as StepX
            _draggingStart = _modelLocation(viewLocation)
            _draggingMarker = _markerFromViewLocation(viewLocation)
            _draggingRegion = _regionFromViewLocation(viewLocation, _mouseStep)
          }

          handleMouseMove(viewLocation)
          event.accept
        }

        case .MOUSE_MOVE {
          handleMouseMove(event.mouseEvent.locationInView(self))
        }

        case .MOUSE_UP {
          handleMouseMove(event.mouseEvent.locationInView(self))
          _draggingScrollbar = null
          _mouseMode = .NONE
        }

        case .MOUSE_SCROLL {
          _handleScroll(event.mouseEvent.delta.x, event.mouseEvent.delta.y)
        }

        case .FOCUS_ENTER {
          _invalidate(.RESET_CARET_BLINK_TIME)
        }

        case .FOCUS_LEAVE {
          if _areCaretsVisible {
            _invalidate(.NORMAL)
            _areCaretsVisible = false
          }
        }

        case .CLIPBOARD_CUT, .CLIPBOARD_COPY {
          var selection = _selection.isEmpty ? selectionExpandedToLines : _selection
          event.clipboardEvent.text = textInSelection(selection)

          if event.type == .CLIPBOARD_CUT {
            changeSelection(selection, .DO_NOT_SCROLL)
            insertText("")
          }
        }

        case .CLIPBOARD_PASTE {
          insertText(event.clipboardEvent.text)
        }

        case .TEXT {
          # Remove old composing text
          if _composingText != null {
            var count = _composingText.count
            var regions List<Region> = []

            # Generate a new selection
            for region in _selection.regions {
              var min = region.min
              var max = region.max
              var line = _model[min.y]
              var minX = min.x - count

              # Double-check that the text we're about to erase is outdated
              if min.x == max.x && min.y == max.y && minX >= 0 && line.text.slice(minX, min.x) == _composingText {
                min = Marker.new(minX, min.y)
              }

              regions.append(Region.new(min, max))
            }

            # Expand the selection before calling insertText() so the old composing text is removed
            changeSelection(Selection.new(regions), .DO_NOT_SCROLL)
          }

          insertText(event.textEvent.text)

          # Record new composing text
          if event.textEvent.isComposing {
            _composingText = event.textEvent.text
          }
        }
      }
    }

    def handleMouseMove(viewLocation Vector) {
      if _draggingScrollbar != null {
        if _draggingScrollbar.continueDragging(viewLocation) {
          _invalidate(.NORMAL)
        }
        window.setCursor(.ARROW)
      }

      else if _mouseMode != .NONE {
        var regions = _mergeBehavior != .NONE ? _previousSelection.regions.clone : List<Region>.new
        var marker = _markerFromViewLocation(viewLocation)

        switch _mouseMode {
          case .SELECTING_CHARACTERS {
            regions.append(_mergeBehavior == .EXTEND
              ? Region.span(regions.takeLast, Region.new(marker))
              : Region.span(_draggingRegion, _regionFromViewLocation(viewLocation, _mouseStep)))
          }

          case .SELECTING_LINES {
            var minY = Math.min(_draggingMarker.y, marker.y)
            var maxY = Math.max(_draggingMarker.y, marker.y)
            var lastLine = _model.count - 1
            var first = Marker.new(0, minY)
            var last = maxY == lastLine ? Marker.new(_model[lastLine].text.count, maxY) : Marker.new(0, maxY + 1)
            var region = marker.y < _draggingMarker.y ? Region.new(last, first) : Region.new(first, last)
            regions.append(_mergeBehavior == .EXTEND ? Region.span(regions.takeLast, Region.new(region.end)) : region)
          }

          case .SELECTING_BOX {
            var draggingEnd = _modelLocation(viewLocation)
            var minX = Math.min(_draggingStart.x, draggingEnd.x)
            var maxX = Math.max(_draggingStart.x, draggingEnd.x)
            var minY = Math.min(_draggingStart.y, draggingEnd.y) as int
            var maxY = Math.max(_draggingStart.y, draggingEnd.y) as int
            var iterator = _iterator
            var first = regions.count
            var hasArea = false

            # Add clamped regions inside the selection box
            for y = minY; y <= maxY; y++ {
              var line = _model[y]
              iterator.reset(line.text)
              iterator.seekForwardToAdvanceWidth(minX)
              var start = Marker.new(iterator.currentIndex, y)
              iterator.seekForwardToAdvanceWidth(maxX)
              var end = Marker.new(iterator.currentIndex, y)
              regions.append(_draggingStart.x == minX ? Region.new(start, end) : Region.new(end, start))
              if start.x != end.x {
                hasArea = true
              }
            }

            # If there's at least one non-empty region, remove all empty
            # regions that were added above
            if hasArea {
              for i = first; i < regions.count; i++ {
                if regions[i].isEmpty {
                  regions.removeAt(i)
                  i--
                }
              }
            }
          }
        }

        changeSelection(Selection.new(regions), .SCROLL_INTO_VIEW)
        window.setCursor(.TEXT)
      }

      else {
        window.setCursor(_scrollbarX.containsPoint(viewLocation) || _scrollbarY.containsPoint(viewLocation) ? .ARROW : .TEXT)
      }

      _previousViewLocation = viewLocation
    }

    def selection Selection {
      return _selection
    }

    def selectionContainingEverything Selection {
      var lastLine = _model.count - 1
      return Selection.new([Region.new(.ZERO, Marker.new(_model[lastLine].text.count, lastLine))])
    }

    def selectionExpandedToLines Selection {
      var lastLine = _model.count - 1
      var newRegions List<Region> = []

      for region in _selection.regions {
        var min = region.min
        var max = region.max
        newRegions.append(Region.new(Marker.new(0, min.y), max.y != lastLine ? Marker.new(0, max.y + 1) : Marker.new(_model[lastLine].text.count, max.y)))
      }

      return Selection.new(newRegions)
    }

    def selectionBrokenIntoLines Selection {
      var newRegions List<Region> = []

      for region in _selection.regions {
        var min = region.min
        var max = region.max

        for y in min.y..(min.y != max.y && max.x == 0 ? max.y : max.y + 1) {
          newRegions.append(Region.new(
            Marker.new(y == min.y ? min.x : 0, y),
            Marker.new(y == max.y ? max.x : _model[y].text.count, y)))
        }
      }

      return Selection.new(newRegions)
    }

    def changeSelection(selection Selection, scroll ScrollBehavior) {
      _selection = selection
      _composingText = null
      _scrollSelectionIntoView(selection, scroll)
      _invalidate(.RESET_CARET_BLINK_TIME)
    }

    def textInSelection(selection Selection) string {
      var regions = selection.regions
      var text = ""
      for i in 0..regions.count {
        if i != 0 {
          text += "\n"
        }
        text += _model.slice(regions[i])
      }
      return text
    }

    def setModel(model Model) {
      if _model != model {
        if _model != null {
          _model.removeObserver(self)
        }
        _model = model
        model.addObserver(self)
        _updateScrollSizeAndInvalidate(.RESET_CARET_BLINK_TIME)
      }
    }

    over render {
      var renderer = window.renderer
      var translateX = _paddingLeft - _scrollbarX.scrollOffset + _marginWidth
      var translateY = _paddingTop - _scrollbarY.scrollOffset
      var minX = _leftPixel
      var maxX = _rightPixel
      var minY = _topLine
      var maxY = _bottomLine + 1
      var regions = _selection.regions
      var iterator = _iterator

      # Make sure syntax highlighting is up to date
      _model.styleLinesIfNeeded(minY, maxY)

      # Clear the background
      renderer.renderRect(0, 0, _viewWidth, _viewHeight, .BACKGROUND_DEFAULT)

      # Render selections
      for region in regions {
        var min = region.min
        var max = region.max
        var startY = Math.max(min.y, minY)
        var endY = Math.min(max.y, maxY)

        for y = startY; y <= endY; y++ {
          var line = _model[y]
          var count = line.text.count
          var startX = y == min.y ? min.x : 0
          var endX = y == max.y ? max.x : count + 1

          if startX != endX {
            var lineY = translateY + _lineHeight * y
            iterator.reset(line.text)
            iterator.seekToIndex(startX)
            var offsetStartX = iterator.advanceWidthFromLeft
            iterator.seekToIndex(endX)
            var offsetEndX = iterator.advanceWidthFromLeft
            if endX == count + 1 {
              offsetEndX += _defaultSpaceAdvanceWidth # Use a trailing space to represent newlines
            }
            renderer.renderRect(translateX + offsetStartX, lineY, offsetEndX - offsetStartX, _lineHeight, .BACKGROUND_SELECTED)

            # Render whitespace characters
            var limit = Math.min(endX, count)
            var spaceStart = startX
            var spaceText = ""
            for x = startX; x <= endX; x++ {
              var c = x < limit ? line.text[x] : 0
              if c == ' ' {
                spaceText += SPACE_STRING
              } else {
                if spaceText != "" {
                  iterator.seekToIndex(spaceStart)
                  renderer.renderText(translateX + iterator.advanceWidthFromLeft, lineY, spaceText, .CODE_FONT, .FOREGROUND_DEFAULT, 0x3F)
                  spaceText = ""
                }
                if c == '\t' {
                  iterator.seekToIndex(x)
                  renderer.renderText(translateX + iterator.advanceWidthFromLeft, lineY, TAB_STRING, .CODE_FONT, .FOREGROUND_DEFAULT, 0x3F)
                }
                spaceStart = x + 1
              }
            }
          }
        }
      }

      # Render lines of text
      for y = minY; y < maxY; y++ {
        var offsetY = translateY + y * _lineHeight
        var line = _model[y]
        var spans = line.spans
        var diagnostics = line.diagnostics

        # Rendering unstyled text is simple
        if spans == null {
          renderer.renderText(translateX, offsetY, line.text, .CODE_FONT, .FOREGROUND_DEFAULT, 0xFF)
        }

        # Render text styled by spans
        else {
          var limit = line.text.count
          var previousOffsetX = 0.0
          var previousEnd = 0

          for span in spans {
            var start = span.start
            var end = span.end

            # Stop after finding a span past the right of the viewport
            if span.offsetX > maxX {
              limit = span.start
              break
            }

            # Skip spans to the left of the viewport
            if span.offsetX + span.advanceWidth >= minX {
              # Render default-styled text between each span
              if previousEnd < start {
                renderer.renderText(translateX + previousOffsetX, offsetY, line.text.slice(previousEnd, start), .CODE_FONT, .FOREGROUND_DEFAULT, 0xFF)
              }

              # Render the styled span text
              renderer.renderText(translateX + span.offsetX, offsetY, line.text.slice(start, end), .CODE_FONT, span.color, 0xFF)
            }

            # Remember the end of this span
            previousOffsetX = span.offsetX + span.advanceWidth
            previousEnd = end
          }

          # Render one last run of unstyled text after the last span
          if previousEnd < limit && translateX + previousOffsetX < maxX {
            renderer.renderText(translateX + previousOffsetX, offsetY, line.text.slice(previousEnd, limit), .CODE_FONT, .FOREGROUND_DEFAULT, 0xFF)
          }
        }

        # Render diagnostics over text
        if diagnostics != null {
          var previousEnd = 0
          for diagnostic in diagnostics {
            var start = diagnostic.start
            var end = Math.max(diagnostic.end, start + 1)

            # Only render diagnostics that don't overlap
            if start >= previousEnd {
              # Measure the diagnostic size
              iterator.reset(line.text)
              iterator.seekToIndex(start)
              var diagnosticMinX = iterator.advanceWidthFromLeft
              iterator.seekToIndex(end)
              var diagnosticMaxX = iterator.advanceWidthFromLeft

              # Render a squiggle under the text if visible
              if diagnosticMaxX >= minX && diagnosticMinX <= maxX {
                renderer.renderSquiggle(translateX + diagnosticMinX,
                  offsetY, diagnosticMaxX - diagnosticMinX, _lineHeight,
                  diagnostic.kind == .ERROR ? .BACKGROUND_DIAGNOSTIC_ERROR : .BACKGROUND_DIAGNOSTIC_WARNING)
                previousEnd = end
              }
            }
          }
        }
      }

      # Render cursors
      var shouldHighlightLineInMargin IntMap<bool> = {}
      for region in regions {
        var marker = region.end
        if minY <= marker.y && marker.y <= maxY {
          if _areCaretsVisible {
            iterator.reset(_model[marker.y].text)
            iterator.seekToIndex(marker.x)
            renderer.renderCaret(translateX + iterator.advanceWidthFromLeft, translateY + _lineHeight * marker.y, .FOREGROUND_CARET)
          }
          shouldHighlightLineInMargin[marker.y] = true
        }
      }

      # Only render line numbers if enabled
      if _marginWidth != 0 {
        # Render the shadow first so it doesn't show up over the margin
        if _scrollbarX.scrollOffset != 0 {
          renderer.renderRightwardShadow(_marginWidth, 0, 20, _viewHeight)
        }

        # Clear the background behind the line number margin
        renderer.renderRect(0, 0, _marginWidth, _viewHeight, .BACKGROUND_MARGIN)

        # Render line numbers
        var baselineAlignment = renderer.fontInstance(.CODE_FONT).size - renderer.fontInstance(.MARGIN_FONT).size
        var rightAlignWidth = _model.count.toString.count
        for y = minY; y < maxY; y++ {
          var text = (y + 1).toString
          var offsetY = translateY + y * _lineHeight
          var isHighlighted = y in shouldHighlightLineInMargin
          if isHighlighted {
            renderer.renderRect(0, offsetY, _marginWidth, _lineHeight, .BACKGROUND_MARGIN_HIGHLIGHTED)
          }
          renderer.renderText(_marginPaddingLeft + _marginSpaceAdvanceWidth * (rightAlignWidth - text.count),
            offsetY + baselineAlignment, text, .MARGIN_FONT, isHighlighted ? .FOREGROUND_MARGIN_HIGHLIGHTED : .FOREGROUND_MARGIN, 0xFF)
        }

        # Render the border on top so the margin highlight doesn't obscure it
        renderer.renderVerticalLine(_marginWidth, 0, _viewHeight, .BORDER_MARGIN)
      }

      # Render lines for errors and warnings under the scrollbars
      for i = 0; i < _model.count; i++ {
        var line = _model[i]
        if line.diagnostics != null {
          renderer.renderHorizontalLine(_viewWidth - _scrollbarThickness, _viewWidth,
            _scrollbarY.mappedPosition(_paddingTop + _lineHeight * (i + 0.5)),
            line.hasErrors ? .BACKGROUND_DIAGNOSTIC_ERROR : .BACKGROUND_DIAGNOSTIC_WARNING)
        }
      }

      # Render the scrollbars
      _scrollbarX.render(renderer)
      _scrollbarY.render(renderer)
    }

    def insertText(text string) {
      var changes List<Change> = []
      for region in _selection.regions {
        var change = Change.new(region, text)
        if change.effect != .NONE {
          changes.append(change)
        }
      }
      _isSelectingInTerminal = false
      _composingText = null
      _model.applyChanges(changes)
      _scrollTo(_selection.max)
    }

    enum InvalidateMode {
      NORMAL
      RESET_CARET_BLINK_TIME
    }

    def _invalidate(mode InvalidateMode) {
      window.invalidate

      if mode == .RESET_CARET_BLINK_TIME {
        _caretBlinkStartTime = window.platform.nowInSeconds
      }
    }

    def handleFrame {
      if window.viewWithFocus != self {
        return
      }

      var areCaretsVisible = window.isActive && ((((window.platform.nowInSeconds - _caretBlinkStartTime) * 2) as int) & 1) == 0

      # Skip rendering if not invalid
      if areCaretsVisible == _areCaretsVisible {
        _idleCount++

        # Render something at 10 fps even when idle otherwise Chrome's
        # render loop goes idle and stutters when starting back up
        if window.platform.userAgent != .CHROME || !window.isActive || _idleCount % 6 != 0 || _idleCount >= 5 * 60 {
          return
        }
      } else {
        _idleCount = 0
      }

      # Carets changed or we're rendering to prevent an idle render loop
      _areCaretsVisible = areCaretsVisible
      window.invalidate
    }

    def handleAction(action Action) {
      if window.viewWithFocus != self {
        return
      }

      var motion MotionAction =
        action.isMoveMotion ? _isSelectingInTerminal ? .EXTEND_SELECTION : .CHANGE_SELECTION :
        action.isSelectMotion ? .EXTEND_SELECTION :
        .DELETE_SELECTION

      # When terminal selection mode is toggled, any other action untoggles it back
      if motion != .EXTEND_SELECTION && action != .SELECT_TOGGLE {
        _isSelectingInTerminal = false
      }

      switch action {
        case .UNDO { _model.undo }
        case .REDO { _model.redo }

        case .SELECT_ALL { changeSelection(selectionContainingEverything, .DO_NOT_SCROLL) }
        case .SELECT_BREAK_INTO_LINES { changeSelection(selectionBrokenIntoLines, .DO_NOT_SCROLL) }
        case .SELECT_EXPAND_TO_LINE { changeSelection(selectionExpandedToLines, .DO_NOT_SCROLL) }
        case .SELECT_FIRST_REGION { _selectFirstRegion }
        case .SELECT_NEXT_DIAGNOSTIC { _selectDiagnostic(.NEXT) }
        case .SELECT_PREVIOUS_DIAGNOSTIC { _selectDiagnostic(.PREVIOUS) }
        case .SELECT_TOGGLE { _isSelectingInTerminal = !_isSelectingInTerminal }

        case .INSERT_CURSOR_ABOVE { _insertCursorY(.PREVIOUS) }
        case .INSERT_CURSOR_BELOW { _insertCursorY(.NEXT) }
        case .INSERT_LINE_ABOVE { _insertLineY(.PREVIOUS) }
        case .INSERT_LINE_BELOW { _insertLineY(.NEXT) }
        case .INSERT_TAB_BACKWARD, .INSERT_TAB_FORWARD { insertText("\t") }

        case .SCROLL_DOWN_DOCUMENT { _scrollTo(selectionContainingEverything.max) }
        case .SCROLL_DOWN_LINE { _handleScroll(0, _lineHeight) }
        case .SCROLL_UP_DOCUMENT { _scrollTo(.ZERO) }
        case .SCROLL_UP_LINE { _handleScroll(0, -_lineHeight) }

        case .MOVE_DOWN_DOCUMENT,   .SELECT_DOWN_DOCUMENT,   .DELETE_DOWN_DOCUMENT   { _triggerActionY(motion, .NEXT, .DOCUMENT) }
        case .MOVE_DOWN_LINE,       .SELECT_DOWN_LINE,       .DELETE_DOWN_LINE       { _triggerActionY(motion, .NEXT, .LINE) }
        case .MOVE_DOWN_PAGE,       .SELECT_DOWN_PAGE,       .DELETE_DOWN_PAGE       { _triggerActionY(motion, .NEXT, .PAGE) }
        case .MOVE_LEFT_CHARACTER,  .SELECT_LEFT_CHARACTER,  .DELETE_LEFT_CHARACTER  { _triggerActionX(motion, .PREVIOUS, .CHARACTER) }
        case .MOVE_LEFT_LINE,       .SELECT_LEFT_LINE,       .DELETE_LEFT_LINE       { _triggerActionX(motion, .PREVIOUS, .LINE) }
        case .MOVE_LEFT_WORD,       .SELECT_LEFT_WORD,       .DELETE_LEFT_WORD       { _triggerActionX(motion, .PREVIOUS, .WORD) }
        case .MOVE_RIGHT_CHARACTER, .SELECT_RIGHT_CHARACTER, .DELETE_RIGHT_CHARACTER { _triggerActionX(motion, .NEXT, .CHARACTER) }
        case .MOVE_RIGHT_LINE,      .SELECT_RIGHT_LINE,      .DELETE_RIGHT_LINE      { _triggerActionX(motion, .NEXT, .LINE) }
        case .MOVE_RIGHT_WORD,      .SELECT_RIGHT_WORD,      .DELETE_RIGHT_WORD      { _triggerActionX(motion, .NEXT, .WORD) }
        case .MOVE_UP_DOCUMENT,     .SELECT_UP_DOCUMENT,     .DELETE_UP_DOCUMENT     { _triggerActionY(motion, .PREVIOUS, .DOCUMENT) }
        case .MOVE_UP_LINE,         .SELECT_UP_LINE,         .DELETE_UP_LINE         { _triggerActionY(motion, .PREVIOUS, .LINE) }
        case .MOVE_UP_PAGE,         .SELECT_UP_PAGE,         .DELETE_UP_PAGE         { _triggerActionY(motion, .PREVIOUS, .PAGE) }
      }
    }

    def handleLexerChange(model Model) {
      _invalidate(.NORMAL)
    }

    def handleDiagnosticChange(model Model) {
      _invalidate(.NORMAL)
    }

    def handleToggleCommit(mode Model, commit Commit, direction CommitDirection) {
      var regions List<Region> = []
      for change in commit.changes {
        var max = change.region.max
        regions.append(Region.new(direction == .UNDO ? change.region.min : max, max))
      }
      changeSelection(Selection.new(regions), .SCROLL_INTO_VIEW)
      _updateScrollSizeAndInvalidate(.RESET_CARET_BLINK_TIME)
    }

    def _markerFromViewLocation(viewLocation Vector) Marker {
      var clampedY = Math.clamp(Math.floor((viewLocation.y + _scrollbarY.scrollOffset - _paddingTop) / _lineHeight) as int, 0, _model.count - 1)
      var mouseX = viewLocation.x + _scrollbarX.scrollOffset - _paddingLeft - _marginWidth
      var iterator = _iterator
      iterator.reset(_model[clampedY].text)
      iterator.seekForwardToAdvanceWidth(mouseX)
      return Marker.new(iterator.currentIndex, clampedY)
    }

    def _regionFromViewLocation(viewLocation Vector, step StepX) Region {
      var marker = _markerFromViewLocation(viewLocation)
      var x = marker.x
      var y = marker.y
      var line = _model[y]

      if step != .CHARACTER {
        return Region.new(
          Marker.new(step == .LINE || !line.isWordBoundary(x, .PREVIOUS) ? line.seekToBoundary(x, .PREVIOUS, step) : x, y),
          Marker.new(step == .LINE || !line.isWordBoundary(x, .NEXT) ? line.seekToBoundary(x, .NEXT, step) : x, y))
      }

      return Region.new(marker)
    }

    def _selectFirstRegion {
      var region = _selection.regions[0]
      changeSelection(Selection.new([region]), .DO_NOT_SCROLL)
      _scrollTo(region.min)
    }

    def _selectDiagnostic(direction Direction) {
      var region = _selection.regions.last
      var marker = direction == .PREVIOUS ? region.min : region.max
      var count = _model.count

      # Check lines in order away from the current line
      for i = 0; i <= count; i++ {
        var y = (marker.y + count + i * direction) % count
        var line = _model[y]
        var diagnostics = line.diagnostics
        if diagnostics == null {
          continue
        }

        # Check the diagnostics on this line in order
        var limit = diagnostics.count
        for j = 0; j < limit; j++ {
          var diagnostic = diagnostics[direction == .PREVIOUS ? limit - j - 1 : j]

          # For the first line, only check in the direction away from the cursor
          if i != 0 || (direction == .PREVIOUS ? diagnostic.start < marker.x : diagnostic.end > marker.x) {
            changeSelection(Selection.new([Region.new(Marker.new(diagnostic.start, y), Marker.new(diagnostic.end, y))]), .CENTER_IF_OUT_OF_VIEW)
            return
          }
        }
      }
    }

    def _insertCursorY(direction Direction) {
      var lastLine = _model.count - 1
      var oldRegions = _selection.regions
      var newRegions = oldRegions.clone

      for region in oldRegions {
        var marker = direction == .PREVIOUS ? region.min : region.max
        var y = Math.clamp(marker.y + (direction == .PREVIOUS ? -1 : 1), 0, lastLine)
        newRegions.append(Region.new(Marker.new(Math.min(marker.x, _model[y].text.count), y)))
      }

      changeSelection(Selection.new(newRegions), .SCROLL_INTO_VIEW)
    }

    def _insertLineY(direction Direction) {
      _triggerActionX(.CHANGE_SELECTION, direction, .LINE)
      if direction == .PREVIOUS {
        _triggerActionX(.CHANGE_SELECTION, .PREVIOUS, .CHARACTER)
      }
      insertText("\n")
    }

    def _triggerActionX(action MotionAction, direction Direction, step StepX) {
      var lastLine = _model.count - 1
      var newRegions List<Region> = []

      for region in _selection.regions {
        var hasArea = !region.isEmpty
        var shouldShrink = hasArea && action == .CHANGE_SELECTION && step != .LINE
        if action == .DELETE_SELECTION && hasArea {
          newRegions.append(region)
          continue
        }
        var end = region.end
        var currentLine = _model[end.y]
        var nextLine = _model[direction == .PREVIOUS ? Math.max(end.y - 1, 0) : Math.min(end.y + 1, lastLine)]
        var seek = currentLine.seekToBoundary(end.x, direction, step)
        end =
          shouldShrink ? direction == .PREVIOUS ? region.min : region.max :
          seek != end.x || step == .LINE ? Marker.new(seek, end.y) :
          direction == .PREVIOUS
          ? end.y != 0 ? Marker.new(nextLine.text.count, end.y - 1) : end
          : end.y != lastLine ? Marker.new(0, end.y + 1) : end
        newRegions.append(Region.new(action != .CHANGE_SELECTION ? region.start : end, end))
      }

      changeSelection(Selection.new(newRegions), .SCROLL_INTO_VIEW)
      if action == .DELETE_SELECTION {
        insertText("")
      }
    }

    def _triggerActionY(action MotionAction, direction Direction, step StepY) {
      var lastLine = _model.count - 1
      var pageSize = Math.round((_viewHeight - _paddingTop - _paddingBottom) / _lineHeight - 1) as int
      var newRegions List<Region> = []

      for region in _selection.regions {
        if action == .DELETE_SELECTION && !region.isEmpty {
          newRegions.append(region)
          continue
        }
        var end = action == .EXTEND_SELECTION ? region.end : direction == .PREVIOUS ? region.min : region.max
        var y =
          direction == .PREVIOUS ?
            step == .LINE ? Math.max(end.y - 1, 0) :
            step == .PAGE ? Math.max(end.y - pageSize, 0) :
            0
          : step == .LINE ? Math.min(end.y + 1, lastLine) :
            step == .PAGE ? Math.min(end.y + pageSize, lastLine) :
            lastLine
        end = Marker.new(
          step != .DOCUMENT && y != end.y ? Math.min(end.x, _model[y].text.count) :
          direction == .PREVIOUS ? 0 : _model[lastLine].text.count,
          y)
        newRegions.append(Region.new(action != .CHANGE_SELECTION ? region.start : end, end))
      }

      changeSelection(Selection.new(newRegions), .SCROLL_INTO_VIEW)

      if action == .DELETE_SELECTION {
        insertText("")
      }
    }

    def _updateScrollSizeAndInvalidate(mode InvalidateMode) {
      if _viewWidth == 0 || _viewHeight == 0 {
        return
      }

      _marginWidth = setting(.SHOW_MARGIN) ? Math.floor(_marginPaddingLeft + _model.count.toString.count * _marginSpaceAdvanceWidth + _marginPaddingRight) : 0

      var scrollWidth = _marginWidth + _paddingLeft + _model.maxAdvanceWidth + _paddingRight
      var scrollHeight = _model.count * _lineHeight + (setting(.SCROLL_PAST_END) ? _viewHeight - _lineHeight : _paddingBottom + _paddingTop)

      _scrollbarX.resize(_viewWidth, scrollWidth)
      _scrollbarY.resize(_viewHeight, scrollHeight)
      _scrollbarX.setPosition(_marginWidth, _viewHeight - _scrollbarThickness, _viewWidth - (_scrollbarY.isNeeded ? _scrollbarThickness : 0), _viewHeight)
      _scrollbarY.setPosition(_viewWidth - _scrollbarThickness, 0, _viewWidth, _viewHeight - (_scrollbarX.isNeeded ? _scrollbarThickness : 0))
      _invalidate(mode)
    }

    def _leftPixel double {
      return _scrollbarX.scrollOffset - _paddingLeft
    }

    def _rightPixel double {
      return _viewWidth - _marginWidth + _scrollbarX.scrollOffset - _paddingRight
    }

    def _topLine int {
      return Math.clamp(Math.floor((_scrollbarY.scrollOffset - _paddingTop) / _lineHeight) as int, 0, _model.count - 1)
    }

    def _bottomLine int {
      return Math.clamp(Math.ceil((_viewHeight + _scrollbarY.scrollOffset - _paddingTop) / _lineHeight) as int - 1, 0, _model.count - 1)
    }

    def _scrollSelectionIntoView(selection Selection, scroll ScrollBehavior) {
      if scroll == .DO_NOT_SCROLL {
        return
      }

      var regions = selection.regions
      var minX = _leftPixel
      var maxX = _rightPixel
      var minY = _topLine
      var maxY = _bottomLine
      var selectionMinX = _model.maxAdvanceWidth
      var selectionMaxX = 0.0
      var selectionMinY = _model.count - 1
      var selectionMaxY = 0

      # Compute a bounding box around the selection
      for region in regions {
        var marker = region.end
        var x = _markerX(marker)
        selectionMinX = Math.min(selectionMinX, x)
        selectionMaxX = Math.max(selectionMaxX, x)
        selectionMinY = Math.min(selectionMinY, marker.y)
        selectionMaxY = Math.max(selectionMaxY, marker.y)
      }

      # Only scroll enough to keep the bounding box in view
      if scroll == .SCROLL_INTO_VIEW {
        if selectionMaxX <= minX { _scrollToX(selectionMaxX) }
        else if selectionMinX >= maxX { _scrollToX(selectionMinX) }

        if selectionMaxY <= minY { _scrollToY(selectionMaxY) }
        else if selectionMinY >= maxY { _scrollToY(selectionMinY) }
      }

      # Scroll the first marker to the center of the view if it's not visible
      else if scroll == .CENTER_IF_OUT_OF_VIEW {
        if selectionMaxX <= minX || selectionMinX >= maxX || selectionMaxY <= minY || selectionMinY >= maxY {
          var marker = regions.first.min
          _scrollbarX.setScrollOffset(_markerX(marker) - (_viewWidth - _paddingLeft - _paddingRight - _marginWidth) / 2)
          _scrollbarY.setScrollOffset(marker.y * _lineHeight - (_viewHeight - _lineHeight - _paddingTop - _paddingBottom) / 2)
          _invalidate(.RESET_CARET_BLINK_TIME)
        }
      }
    }

    def _scrollTo(marker Marker) {
      _scrollToX(_markerX(marker))
      _scrollToY(marker.y)
    }

    def _scrollToX(x double) {
      var minX = _leftPixel
      var maxX = _rightPixel
      var offsetX = _scrollbarX.scrollOffset

      if x <= minX {
        offsetX = x
      } else if x >= maxX {
        offsetX = x - (_viewWidth - _paddingLeft - _paddingRight - _marginWidth)
      }

      if _scrollbarX.setScrollOffset(offsetX) {
        _invalidate(.RESET_CARET_BLINK_TIME)
      }
    }

    def _scrollToY(y int) {
      var minY = _topLine
      var maxY = _bottomLine
      var offsetY = _scrollbarY.scrollOffset

      if y <= minY {
        offsetY = y * _lineHeight
      } else if y >= maxY {
        # Use max to ensure offsetY doesn't go backwards, which it would otherwise do with SCROLL_PAST_END on
        offsetY = Math.max(offsetY, (y + 1) * _lineHeight - (_viewHeight - _paddingTop - _paddingBottom))
      }

      if _scrollbarY.setScrollOffset(offsetY) {
        _invalidate(.RESET_CARET_BLINK_TIME)
      }
    }

    def _markerX(marker Marker) double {
      var iterator = _iterator
      iterator.reset(_model[marker.y].text)
      iterator.seekToIndex(marker.x)
      return iterator.advanceWidthFromLeft
    }
  }

  namespace View {
    const SPACE_CHARACTER = 0xB7
    const TAB_CHARACTER = 0x2192
    const SPACE_STRING = string.fromCodePoint(SPACE_CHARACTER)
    const TAB_STRING = string.fromCodePoint(TAB_CHARACTER)
  }
}
