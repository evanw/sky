namespace Browser {
  class CanvasElementWebGL : CanvasElement {
    var _context Context = null
    var _solidBatch Graphics.SolidBatch = null
    var _glyphBatch Graphics.GlyphBatch = null
    var _dropShadow Graphics.DropShadow = null
    var _clearColor Graphics.RGBA = .TRANSPARENT

    def new(window Window) {
      super(window)

      var gl WebGLRenderingContext = null
      var options dynamic = {
        "alpha": false,
        "antialias": false,
        "depth": false,
        "failIfMajorPerformanceCaveat": true,
        "preserveDrawingBuffer": true,
        "stencil": false,
      }

      # Attempt to use the official WebGL string
      try {
        gl = _element.getContext("webgl", options)
      }

      # Fall back to the older unofficial WebGL string
      gl ?= _element.getContext("experimental-webgl", options)

      _context = Context.new(gl)
      _solidBatch = Graphics.SolidBatch.new(_context)
      _glyphBatch = Graphics.GlyphBatch.new(_context)
      _dropShadow = Graphics.DropShadow.new(_context)

      Log.info("initialized WebGL canvas")
    }

    over setDefaultBackgroundColor(color Graphics.RGBA) {
      _glyphBatch.setBackgroundColor(color)
      _clearColor = color
    }

    over setViewport(x double, y double, width double, height double) {
      _solidBatch.flush
      _glyphBatch.flush

      _context.setViewport(
        Math.round(x * _pixelScale) as int,
        Math.round(y * _pixelScale) as int,
        Math.round(width * _pixelScale) as int,
        Math.round(height * _pixelScale) as int)

      _solidBatch.resize(width, height, _pixelScale)
      _glyphBatch.resize(width, height, _pixelScale)
      _dropShadow.resize(width, height)
    }

    over _afterResize {
      _context.resize(_element.width, _element.height)
      setViewport(0, 0, _width, _height)
    }

    over beginFrame {
      super
      _context.clear(_clearColor)
    }

    over endFrame {
      super
      _solidBatch.flush
      _glyphBatch.flush
    }

    over fillRect(x double, y double, width double, height double, color Graphics.RGBA) {
      assert(_isRendering)

      if x >= _width || y >= _height || x + width <= 0 || y + height <= 0 {
        return
      }

      _glyphBatch.flush
      _solidBatch.fillRect(x, y, width, height, color.premultiplied)
    }

    over fillRoundedRect(x double, y double, width double, height double, color Graphics.RGBA, radius double) {
      assert(_isRendering)

      if x >= _width || y >= _height || x + width <= 0 || y + height <= 0 {
        return
      }

      _glyphBatch.flush
      _solidBatch.fillRoundedRect(x, y, width, height, color.premultiplied, radius)
    }

    over strokePolyline(coordinates List<double>, color Graphics.RGBA, thickness double) {
      assert(_isRendering)

      assert(coordinates.count % 2 == 0)
      _glyphBatch.flush
      _solidBatch.strokeNonOverlappingPolyline(coordinates, color.premultiplied, thickness, .OPEN)
    }

    over renderRectShadow(
      rectX double, rectY double, rectWidth double, rectHeight double,
      clipX double, clipY double, clipWidth double, clipHeight double,
      shadowAlpha double, blurSigma double) {

      assert(_isRendering)

      if clipX >= _width || clipY >= _height || clipX + clipWidth <= 0 || clipY + clipHeight <= 0 {
        return
      }

      _solidBatch.flush
      _glyphBatch.flush
      _dropShadow.render(rectX, rectY, rectWidth, rectHeight, clipX, clipY, clipWidth, clipHeight, shadowAlpha, blurSigma)
    }

    over renderText(x double, y double, text string, font UI.Font, color Graphics.RGBA) {
      assert(_isRendering)

      var fontInstance = _fontInstances.get(font, null)
      if fontInstance == null || x >= _width || y >= _height || y + fontInstance.size <= 0 {
        return
      }

      var iterator = Unicode.StringIterator.INSTANCE.reset(text, 0)

      _solidBatch.flush
      color = color.premultiplied

      for codePoint = iterator.nextCodePoint; codePoint != -1; codePoint = iterator.nextCodePoint {
        x += _glyphBatch.appendGlyph(fontInstance, codePoint, x, y, color)
      }
    }

    over _fontInstanceScale double {
      return _pixelScale
    }
  }
}
